buildscript {
	ext {
		set('springCloudVersion', "Hoxton.SR1")
		set('springBootVersion', "2.2.2.RELEASE")
		set('lombokPluginVersion', "3.2.0")
	}

	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE")
		classpath("io.franzbecker:gradle-lombok:${lombokPluginVersion}")
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: "io.franzbecker.gradle-lombok"

	group = 'com.nonoplan'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	test {
		useJUnitPlatform()
	}

//	task initSourceFolders {
//		sourceSets*.java.srcDirs*.each {
//			if (!it.exists()) {
//				it.mkdirs()
//			}
//		}
//
//		sourceSets*.resources.srcDirs*.each {
//			if (!it.exists()) {
//				it.mkdirs()
//			}
//		}
//	}
}

project(':core') {
	version = "1.0.0"

	bootJar {
		enabled = false
	}

	jar {
		archivesBaseName = "core"
		enabled = true
	}

	sourceSets {
		main {
			resources {
				srcDirs = ['src/main/java', 'src/main/resources']
				exclude '**/*.java'
			}
		}
	}

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-gcp-starter'
		implementation 'org.springframework.cloud:spring-cloud-gcp-starter-sql-mysql'
	}
}

project(':batch') {

	sourceSets {
		main {
			resources {
				srcDirs = ['src/main/java', 'src/main/resources']
				exclude '**/*.java'
			}
		}
	}

	dependencies {
		implementation project(':core')
		implementation 'org.springframework.boot:spring-boot-starter-batch'
	}
}